Petite Golden Crocodile

high

# `AlchemicTokenV2Base::flashLoan` misses `onlyWhitelisted` and `paused` checks

## Summary

Anyone can perform flashLoan over the `AlchemicToken`, even if he is not whitelisted or is paused to mint.

## Vulnerability Detail

When user try to mint AlchemicToken there are 2 check that are performed:

- He must be whitelisted to mint (be in the `whitelisted` mapping)
- Must not be in the `paused` mapping

```solidity
/// @notice A set of addresses which are whitelisted for minting new tokens.
mapping(address => bool) public whitelisted;

/// @notice A set of addresses which are paused from minting new tokens.
mapping(address => bool) public paused;
```

```solidity
function mint(address recipient, uint256 amount) external onlyWhitelisted {
  if (paused[msg.sender]) {
    revert IllegalState();
  }

  // If bridge is registered check limits and adjust them accordingly.
  if (xBridges[msg.sender].minterParams.maxLimit > 0) {
    uint256 currentLimit = mintingCurrentLimitOf(msg.sender);
    if (amount > currentLimit) revert IXERC20.IXERC20_NotHighEnoughLimits();
    _useMinterLimits(msg.sender, amount);
  }

  _mint(recipient, amount);
}
```

However, `AlchemicTokenV2Base::flashLoan` do not check if the caller(`msg.sender`) or the `ERC3156FlashBorrower receiver` is allowed to mint tokens. These tokens will be burned at the end, but when misses this checks, it totally defeats the whole purpose of them.

```solidity
function flashLoan(
  IERC3156FlashBorrower receiver,
  address token,
  uint256 amount,
  bytes calldata data
) external override nonReentrant returns (bool) {
  if (token != address(this)) {
    revert IllegalArgument();
  }

  if (amount > maxFlashLoan(token)) {
    revert IllegalArgument();
  }

  uint256 fee = flashFee(token, amount);

  _mint(address(receiver), amount);

  if (receiver.onFlashLoan(msg.sender, token, amount, fee, data) != CALLBACK_SUCCESS) {
    revert IllegalState();
  }

  _burn(address(receiver), amount + fee); // Will throw error if not enough to burn

  return true;
}
```

## Impact

Any user can perform token flashLoan, even if he is not whitelisted or paused to mint tokens.

## Code Snippet

https://github.com/sherlock-audit/2024-04-alchemix/blob/32e4902f77b05ea856cf52617c55c3450507281c/v2-foundry/src/AlchemicTokenV2Base.sol#L247-L272

## Tool used

Manual Review

## Recommendation

Check if the caller is allowed to mint tokens that way the checks will have value at all.

```diff
function flashLoan(
  IERC3156FlashBorrower receiver,
  address token,
  uint256 amount,
  bytes calldata data
- ) external override nonReentrant returns (bool) {
+ ) external override nonReentrant onlyWhitelisted returns (bool) {
  if (token != address(this)) {
    revert IllegalArgument();
  }
  
+ if (paused[msg.sender]) {
+     revert IllegalState();
+ }

  if (amount > maxFlashLoan(token)) {
    revert IllegalArgument();
  }

  uint256 fee = flashFee(token, amount);

  _mint(address(receiver), amount);

  if (receiver.onFlashLoan(msg.sender, token, amount, fee, data) != CALLBACK_SUCCESS) {
    revert IllegalState();
  }

  _burn(address(receiver), amount + fee); // Will throw error if not enough to burn

  return true;
}
```