Old Leather Lemur

high

# MEV attack on claimAndDonateRewards function

## Summary
Anyone can call [OptimismRewardCollector.claimAndDonateRewards()](https://github.com/sherlock-audit/2024-04-alchemix/blob/32e4902f77b05ea856cf52617c55c3450507281c/v2-foundry/src/utils/collectors/OptimismRewardCollector.sol#L57) and it will swap reward token into debt tokens. The slippage parameter `minimumAmountOut` can be 0. Meaning, it can be used to create an MEV opputunity.


## Vulnerability Detail

```solidity
@>  function claimAndDonateRewards(address token, uint256 minimumAmountOut) external returns (uint256) {
        uint256 amountRewardToken = IERC20(rewardToken).balanceOf(address(this));

        if (amountRewardToken == 0) return 0;

        if (debtToken == 0xCB8FA9a76b8e203D8C3797bF438d8FB81Ea3326A) {
            // Velodrome Swap Routes: OP -> USDC -> alUSD
            IVelodromeSwapRouter.route[] memory routes = new IVelodromeSwapRouter.route[](2);
            routes[0] = IVelodromeSwapRouter.route(0x4200000000000000000000000000000000000042, 0x7F5c764cBc14f9669B88837ca1490cCa17c31607, false);
            routes[1] = IVelodromeSwapRouter.route(0x7F5c764cBc14f9669B88837ca1490cCa17c31607, 0xCB8FA9a76b8e203D8C3797bF438d8FB81Ea3326A, true);
            TokenUtils.safeApprove(rewardToken, swapRouter, amountRewardToken);
@>          IVelodromeSwapRouter(swapRouter).swapExactTokensForTokens(amountRewardToken, minimumAmountOut, routes, address(this), block.timestamp);
        } else if (debtToken == 0x3E29D3A9316dAB217754d13b28646B76607c5f04) {
            // Velodrome Swap Routes: OP -> alETH
            IVelodromeSwapRouter.route[] memory routes = new IVelodromeSwapRouter.route[](1);
            routes[0] = IVelodromeSwapRouter.route(0x4200000000000000000000000000000000000042, 0x3E29D3A9316dAB217754d13b28646B76607c5f04, false);
            TokenUtils.safeApprove(rewardToken, swapRouter, amountRewardToken);
@>          IVelodromeSwapRouter(swapRouter).swapExactTokensForTokens(amountRewardToken, minimumAmountOut, routes, address(this), block.timestamp);
        } else {
            revert IllegalState("Reward collector `debtToken` is not supported");
        }

        // Donate to alchemist depositors
        uint256 debtReturned = IERC20(debtToken).balanceOf(address(this));
        TokenUtils.safeApprove(debtToken, alchemist, debtReturned);
        IAlchemistV2(alchemist).donate(token, debtReturned);

        return amountRewardToken;
    }
```

**Attack path:**
All the below actions happens in one block.
1. when reward token is sold to buy debt tokens by calling `claimAndDonateRewards`, before this action, buy the debt token. So an attacker bought x amount at price A.
2. Then call `claimAndDonateRewards` which will increase the price of that debt token to `A + 5%` depends on liquidity and reward amount that got swapped.
3. So now sell the debt token taking profits off the price different between price `A + 5%` and price `A`.

To cause maximum damage, 
    1. buy as much debt token as possible.
    2. then call `claimAndDonateRewards` with `minimumAmountOut = 0`, and it will return 0 debt amount because the price of that debt token is so high, because someone inflated it already.
    3. Now sell all the debt token and stabilize the pool, so here impact is on protocol, that it will get 0 debt tokens, and attacker will only lose fees and some slippage. But if attacker wants to make profit and also an attack, then the first path will be chosen to inflate only 5 - 10%.

## Impact
Loss of rewards and easy attack vector. So its an high.

## Code Snippet

https://github.com/sherlock-audit/2024-04-alchemix/blob/32e4902f77b05ea856cf52617c55c3450507281c/v2-foundry/src/utils/collectors/OptimismRewardCollector.sol#L57


## Tool used

Manual Review

## Recommendation

Make [OptimismRewardCollector.claimAndDonateRewards()](https://github.com/sherlock-audit/2024-04-alchemix/blob/32e4902f77b05ea856cf52617c55c3450507281c/v2-foundry/src/utils/collectors/OptimismRewardCollector.sol#L57), an `onlyOwner` function. So only team can periodically claim and swap the reward tokens.