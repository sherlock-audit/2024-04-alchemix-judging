Gigantic Holographic Worm

high

# Re-entrancy in distributeRewards allows for immediate payout of all rewards

## Summary
Re-entrancy in `RewardRouter::distributeRewards()` allows for preliminary distribution of all rewards instead of linear distribution as intended.

## Vulnerability Detail
The contract in scope are used to distribute rewards to vaults linearly depending on `block.number`. In other words if X rewards are to be distributed in 100 blocks and 50 blocks have already passed - (X*50)/100 rewards are available for claim at current time. this is present in the `distributeRewards()` function:
```solidity
      uint256 blocksSinceLastReward = block.number - rewards[vault].lastRewardBlock;
      uint256 maxReward = rewards[vault].rewardAmount - rewards[vault].rewardPaid;
      uint256 currentReward = rewards[vault].rewardAmount * blocksSinceLastReward / rewards[vault].rewardTimeframe;
      uint256 amountToSend = currentReward > maxReward ? maxReward : currentReward;
```
Reward is then transfered to the collector  
```solidity
TokenUtils.safeTransfer(IRewardCollector(rewards[vault].rewardCollectorAddress).rewardToken(), rewards[vault].rewardCollectorAddress, amountToSend);
```
and finally `lastRewardBlock` and `rewardPaid` are updated
```solidity
      rewards[vault].lastRewardBlock = block.number;
      rewards[vault].rewardPaid += amountToSend;
```
However this not ok, because a collector could re-enter the `distributeRewards` function, after the `safeTransfer` call
What happens is the `lastRewardBlock` and `rewardPaid` are not yet updated so more tokens are transfered and this loops untill the reward is drained.

Another way to re-enter would be with reward tokens having callback function like ERC777 which is plausible

## Impact
Stealing all rewards accumulated instantly

## Code Snippet
https://github.com/sherlock-audit/2024-04-alchemix/blob/main/v2-foundry/src/utils/RewardRouter.sol#L35-L56

## Tool used
Manual Review

## Recommendation
Move the setting of `lastRewardBlock` and `rewardPaid` before the transfer tokens call.
```diff
  function distributeRewards(address vault) external returns (uint256) {
      // If vault is set to receive rewards from grants, send amount to reward collector to donate
      if (rewards[vault].rewardAmount > 0) {
          // Calculates ratio of timeframe to time since last harvest
          // Uses this ratio to determine partial reward amount or extra reward amount
          uint256 blocksSinceLastReward = block.number - rewards[vault].lastRewardBlock;
          uint256 maxReward = rewards[vault].rewardAmount - rewards[vault].rewardPaid;
          uint256 currentReward = rewards[vault].rewardAmount * blocksSinceLastReward / rewards[vault].rewardTimeframe;
          uint256 amountToSend = currentReward > maxReward ? maxReward : currentReward;

+          rewards[vault].lastRewardBlock = block.number;
+          rewards[vault].rewardPaid += amountToSend;

          TokenUtils.safeTransfer(IRewardCollector(rewards[vault].rewardCollectorAddress).rewardToken(), rewards[vault].rewardCollectorAddress, amountToSend);
-          rewards[vault].lastRewardBlock = block.number;
-          rewards[vault].rewardPaid += amountToSend;

          if (rewards[vault].rewardPaid == rewards[vault].rewardAmount) {
              rewards[vault].rewardAmount = 0;
              rewards[vault].rewardPaid = 0;
          }
      }
```

