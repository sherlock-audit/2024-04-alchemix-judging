Genuine Umber Seal

medium

# CrossChainCanonicalBase minted limitation can be reached after lots of exchangeOldForCanonical/exchangeCanonicalForOld actions

## Summary

`CrossChainCanonicalBase` contract `totalMinted` limitation can be reached because unconsidered scenarios after lots of `exchangeOldForCanonical/exchangeCanonicalForOld` actions be executed.


## Vulnerability Detail

When executing `CrossChainCanonicalBase#exchangeOldForCanonical` function, it first judge the total token minted limitation is reached to `mintCeiling[bridgeTokenAddress]` or not, if reached it will be reverted. After that, it will subtract the swap fee and then mint canonical tokens and give it to the sender.

```solidity
function exchangeOldForCanonical() {
    ...
    canonicalTokensOut = tokenAmount;
    // @audit - subtract the swap fee based on `swapFees[bridgeTokenAddress][0]`
    if (!_isFeeExempt(msg.sender)) {
>>>        canonicalTokensOut -= ((canonicalTokensOut * swapFees[bridgeTokenAddress][0]) / FEE_PRECISION);
    }

    // Mint canonical tokens and give it to the sender
    // @audit - then mint the left canonical tokens to sender
>>>    super._mint(msg.sender, canonicalTokensOut);
}
```

When executing `CrossChainCanonicalBase#exchangeCanonicalForOld` function, it also subtract the needed swap fee based on `swapFees[bridgeTokenAddress][1]`, then subtract the `tokenAmount` from `totalMinted[bridgeTokenAddress]` and transfer the old tokens to user.

```solidity
function exchangeCanonicalForOld(address bridgeTokenAddress, uint256 tokenAmount) external nonReentrant validBridgeToken(bridgeTokenAddress) returns (uint256 bridgeTokensOut) {
    ...
    // Burn the canonical tokens
    super._burn(msg.sender, tokenAmount);

    // Handle the fee, if applicable
    bridgeTokensOut = tokenAmount;
    if (!_isFeeExempt(msg.sender)) {
        bridgeTokensOut -= ((bridgeTokensOut * swapFees[bridgeTokenAddress][1]) / FEE_PRECISION);
    }

    // Update mint count
    totalMinted[bridgeTokenAddress] -= tokenAmount;

    // Give old tokens to the sender
    TokenUtils.safeTransfer(bridgeTokenAddress, msg.sender, bridgeTokensOut);
    }
```

So the `totalMinted[bridgeTokenAddress]` will become greater because swap fee can be accumulated when executing `CrossChainCanonicalBase#exchangeOldForCanonical` function.

For example, from `CrossChainCanonicalBase#__CrossChainCanonicalBase_init` we can see, the `swapFees[_bridgeTokens]` can be set to `[400, 400]` at first, we use the default fee as example:

1. users mint some tokens after transfer some `bridgeToken` to the contract, the `tokenAmount` is `1 ether`, `mintCeiling[bridgeTokenAddress]` is set to 10 ether. `swap fee = tokenAmount * 0.04% = 0.0004 ether`, `canonicalTokensOut = 1 ether - 0.0004 ether = 0.9996 ether`, and `totalMinted[bridgeTokenAddress] = 1 ether`
2. users call `exchangeCanonicalForOld` to exchange for old tokens with `0.9996 ether` tokenAmount minted at step1, the `totalMinted[bridgeTokenAddress] -= 0.9996 ether = 0.0004 ether`, which is the swap fee at step1.
3. users repeat step1 and step2, noted that users are just use the functions as normal, but the `totalMinted[bridgeTokenAddress]` will become greater little by little because swap fee not be subtracted from `totalMinted[bridgeTokenAddress]`, and protocol has no any mechanism to subtract the swap fee that recorded in `totalMinted[bridgeTokenAddress]`.
4. About ~22501(22501 * 0.0004 + 1 = 10.0004 ether > 10 ether) times later, the `totalMinted[bridgeTokenAddress]` will be reached the `mintCeiling[bridgeTokenAddress]` limitation, any users can't mint any more tokens.


Even the admin set the `mintCeiling[bridgeTokenAddress]` to a higher value, the `totalMinted[bridgeTokenAddress]` will reach the new limitation in one day, so it's not a good solution.

Below code mock same scenario as a example to show the issue:

```solidity
contract DemotTest is Test {

    constructor() payable {}

    function setUp() public {

    }

    uint256 canonicalTokensOut = 1 ether;
    uint256 FEE = 400;
    mapping(address => uint256) public totalMinted;
    mapping(address => uint256) public userMinted;
    address alice = makeAddr("alice");

    uint256 mintLimit = 10 ether;
    address bridgeToken = address(0x123);

    function testDemo() public {
        for (uint i = 0; i < 30000; i++) {
            mint();
            burn(userMinted[alice]);
            console.log("i: %s, totalMinted[address(bridgeToken)]: %s", i, totalMinted[address(bridgeToken)]);
        }
    }

    function mint() public {
        uint256 canonicalTokensOutFee = canonicalTokensOut * FEE / 1e6;
        console.log("canonicalTokensOutFee: ", canonicalTokensOutFee);
        console.log("alice canonicalTokensOut: ", canonicalTokensOut - canonicalTokensOutFee);
        
        uint256 total = totalMinted[bridgeToken] + canonicalTokensOut;
        if (total > mintLimit) {
            revert("reach limit");
        }
        totalMinted[address(bridgeToken)] += canonicalTokensOut;
        userMinted[alice] = canonicalTokensOut - canonicalTokensOutFee;
    }

    function burn(uint256 amount) public {
        totalMinted[address(bridgeToken)] -= amount;
    }
}
```

Results:

```md
  alice canonicalTokensOut:  999600000000000000
  i: 25000, totalMinted[address(bridgeToken)]: 10000400000000000000
  canonicalTokensOutFee:  400000000000000
  alice canonicalTokensOut:  999600000000000000

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 561.77ms (547.93ms CPU time)
```


## Impact

The minted limitation can be reached after many `exchangeCanonicalForOld/exchangeOldForCanonical` txs and users can't mint any more tokens.


## Code Snippet

https://github.com/sherlock-audit/2024-04-alchemix/blob/32e4902f77b05ea856cf52617c55c3450507281c/v2-foundry/src/CrossChainCanonicalBase.sol#L92-L145


## Tool used

vscode, Manual Review

## Recommendation

Protocol should use another mapping to record the swap fee every time users execute `exchangeCanonicalForOld/exchangeOldForCanonical` functions, when admin call `CrossChainCanonicalBase#recoverERC20` to claim the swap fee, the swap fee should also be subtracted from the swap fee mapping every time to make sure no dirty data there.
