Furry Hazel Salmon

medium

# Potential DoS in `distributeRewards()` due to Strict Enforcement of Price Feed Validity

## Summary
The function `getExpectedExchange()` verifies the validity of OP/USD and ETH/USD price feeds. This strict enforcement can potentially lead to a temporary DoS scenario in `distributeRewards()` when `debtToken == alUSD` and the ETH/USD feed returns incorrect or stale data.

## Vulnerability Detail
Within the `distributeRewards()` function, rewards are distributed, and the reward collector is triggered to claim and donate. This involves a call to `OptimismRewardCollector.getExpectedExchange()` to simulate the exchange rate for calculating slippage protection (`minimumAmountOut`).
```solidity
    function distributeRewards(address vault) external returns (uint256) {
        // If vault is set to receive rewards from grants, send amount to reward collector to donate
        if (rewards[vault].rewardAmount > 0) {
            // Calculates ratio of timeframe to time since last harvest
            // Uses this ratio to determine partial reward amount or extra reward amount
            uint256 blocksSinceLastReward = block.number - rewards[vault].lastRewardBlock;
            uint256 maxReward = rewards[vault].rewardAmount - rewards[vault].rewardPaid;
            uint256 currentReward = rewards[vault].rewardAmount * blocksSinceLastReward / rewards[vault].rewardTimeframe;
            uint256 amountToSend = currentReward > maxReward ? maxReward : currentReward;

            TokenUtils.safeTransfer(IRewardCollector(rewards[vault].rewardCollectorAddress).rewardToken(), rewards[vault].rewardCollectorAddress, amountToSend);
            rewards[vault].lastRewardBlock = block.number;
            rewards[vault].rewardPaid += amountToSend;

            if (rewards[vault].rewardPaid == rewards[vault].rewardAmount) {
                rewards[vault].rewardAmount = 0;
                rewards[vault].rewardPaid = 0;
            }
        }

      ->return IRewardCollector(rewards[vault].rewardCollectorAddress).claimAndDonateRewards(vault, IRewardCollector(rewards[vault].rewardCollectorAddress).getExpectedExchange(vault) * slippageBPS / BPS);
    }
```
It's important to note that `OptimismRewardCollector.sol` supports two types of debtToken: alUSD and alETH. In `getExpectedExchange()`, it retrieves price feeds from Chainlink Oracle for OP/USD and ETH/USD.

The function checks the validity of the price feeds by verifying the `answer` and staleness of each.
```solidity
    function getExpectedExchange(address yieldToken) external view returns (uint256) {
        uint256 expectedExchange;
        address[] memory token = new address[](1);
        uint256 totalToSwap = TokenUtils.safeBalanceOf(rewardToken, address(this));

        // Ensure that round is complete, otherwise price is stale.
        (
            uint80 roundID,
            int256 opToUsd,
            ,
            uint256 updateTime,
            uint80 answeredInRound
        ) = IChainlinkOracle(opToUsdOracle).latestRoundData();
        
      ->require(
            opToUsd > 0, 
            "Chainlink Malfunction"
        );

      ->if( updateTime < block.timestamp - 1200 seconds ) {
            revert("Chainlink Malfunction");
        }

        // Ensure that round is complete, otherwise price is stale.
        (
            uint80 roundIDEth,
            int256 ethToUsd,
            ,
            uint256 updateTimeEth,
            uint80 answeredInRoundEth
        ) = IChainlinkOracle(ethToUsdOracle).latestRoundData();
        
      ->require(
            ethToUsd > 0, 
            "Chainlink Malfunction"
        );

      ->if( updateTimeEth < block.timestamp - 1200 seconds ) {
            revert("Chainlink Malfunction");
        }

        // Find expected amount out before calling harvest
        if (debtToken == alUsdOptimism) {
            expectedExchange = totalToSwap * uint(opToUsd) / 1e8;
        } else if (debtToken == alEthOptimism) {
            expectedExchange = totalToSwap * uint(uint(opToUsd)) / uint(ethToUsd);
        } else {
            revert IllegalState("Invalid debt token");
        }

        return expectedExchange;
    }
```
In the case where `debtToken == alUSD`, the ETH/USD feed is unnecessary to calculate the conversion rate. However, the function strictly enforces the validity of both price feeds, potentially leading to a temporary DoS in `distributeRewards()` if the ETH/USD feed returns stale or incorrect data.

## Impact
Potential DoS when `debtToken == alUSD`
## Code Snippet
https://github.com/sherlock-audit/2024-04-alchemix/blob/main/v2-foundry/src/utils/RewardRouter.sol#L35

https://github.com/sherlock-audit/2024-04-alchemix/blob/main/v2-foundry/src/utils/collectors/OptimismRewardCollector.sol#L91

## Tool used
Manual Review

## Recommendation
```solidity
        function getExpectedExchange(address yieldToken) external view returns (uint256) {
        uint256 expectedExchange;
        address[] memory token = new address[](1);
        uint256 totalToSwap = TokenUtils.safeBalanceOf(rewardToken, address(this));

        (
            uint80 roundID,
            int256 opToUsd,
            ,
            uint256 updateTime,
            uint80 answeredInRound
        ) = IChainlinkOracle(opToUsdOracle).latestRoundData();
        
        require(
            opToUsd > 0, 
            "Chainlink Malfunction"
        );

        if( updateTime < block.timestamp - 1200 seconds ) {
            revert("Chainlink Malfunction");
        }

-       (
-           uint80 roundIDEth,
-           int256 ethToUsd,
-           ,
-           uint256 updateTimeEth,
-           uint80 answeredInRoundEth
-       ) = IChainlinkOracle(ethToUsdOracle).latestRoundData();
        
-       require(
-           ethToUsd > 0, 
-           "Chainlink Malfunction"
-       );

-       if( updateTimeEth < block.timestamp - 1200 seconds ) {
-           revert("Chainlink Malfunction");
-       }

        if (debtToken == alUsdOptimism) {
            expectedExchange = totalToSwap * uint(opToUsd) / 1e8;
        } else if (debtToken == alEthOptimism) {
+           (
+               uint80 roundIDEth,
+               int256 ethToUsd,
+               ,
+               uint256 updateTimeEth,
+               uint80 answeredInRoundEth
+           ) = IChainlinkOracle(ethToUsdOracle).latestRoundData();
        
+           require(
+               ethToUsd > 0, 
+               "Chainlink Malfunction"
+           );

+           if( updateTimeEth < block.timestamp - 1200 seconds ) {
+               revert("Chainlink Malfunction");
+           }

            expectedExchange = totalToSwap * uint(uint(opToUsd)) / uint(ethToUsd);
        } else {
            revert IllegalState("Invalid debt token");
        }

        return expectedExchange;
    }
```